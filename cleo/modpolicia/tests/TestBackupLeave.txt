{$CLEO .csa}
03A4: script_name 'Test'

// Spawns a ped, a car, makes the ped enter the car and drive away

0000:
0@ = 0
jump @PRELOAD
 
//----------------------------------------
:MAIN
wait 0

if 
    0256: player $PLAYER_CHAR defined
jf @MAIN

if
    0A51: is_widget_pressed 76 //left arrow small
then
    0@ += 1
    
    if 0@ == 1
    then
        02FF: show_text_3numbers GXT 'MPFX1' numbers 1 0 0 time 1000 flag 1
        
        04C4: store_coords_to 1@ 2@ 3@ from_actor $PLAYER_ACTOR with_offset 0.0 10.0 0.0
        
        009A: $PED = create_actor_pedtype 4 model 280 at 1@ 2@ 3@ 

        04C4: store_coords_to 1@ 2@ 3@ from_actor $PLAYER_ACTOR with_offset 0.0 10.0 0.0
        
        00A5: $CAR = create_car 596 at 1@ 2@ 3@
        
        020A: set_car $CAR door_status_to 1 
        
        wait 1000
    end
    
    if 0@ == 2
    then
        02FF: show_text_3numbers GXT 'MPFX1' numbers 2 0 0 time 1000 flag 1
        
        05CB: AS_actor $PED enter_car $CAR as_driver 10000 ms 
        
        wait 1000
    end
    
    if 0@ == 3
    then
        02FF: show_text_3numbers GXT 'MPFX1' numbers 3 0 0 time 1000 flag 1
        
        04C4: store_coords_to 1@ 2@ 3@ from_actor $PLAYER_ACTOR with_offset 0.0 100.0 0.0
        
        //00A7: car $CAR drive_to 1@ 2@ 3@
        
        05D1: AS_actor $PED drive_car $CAR to 1@ 2@ 3@ speed 15.0 0 model #NULL 0 
 
        wait 1000
    end
    
    if 0@ == 4
    then
       0@ = 0
    end
end 
   
jump @MAIN

//----------------------------------------

:PRELOAD

0247: load_model 280
0247: load_model 596
038B: load_requested_models

jump @PRELOAD_WAIT

//----------------------------------------

:PRELOAD_WAIT

if and
    0248: model 280 available
    0248: model 596 available
jf @PRELOAD_WAIT

jump @MAIN